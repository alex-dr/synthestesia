/**
  * This sketch demonstrates how to use an FFT to analyze
  * the audio being generated by an AudioPlayer.
  * <p>
  * FFT stands for Fast Fourier Transform, which is a
  * method of analyzing audio that allows you to visualize
  * the frequency content of a signal. You've seen
  * visualizations like this before in music players
  * and car stereos.
  * <p>
  * For more information about Minim and additional features,
  * visit http://code.compartmental.net/minim/
  */

import ddf.minim.analysis.*;
import ddf.minim.*;

Minim       minim;
Song song;
String windowName;


int FFT_SAMPLES = 1024 * 2;
int BAR_WIDTH;

void setup()
{
    size(2048, 800, P3D);

    minim = new Minim(this);
    song = new Song(minim, "example-music/bensound-cute.mp3", FFT_SAMPLES);
    Thread songthread = new Thread(song);
    songthread.start();

    BAR_WIDTH = 2*width / song.song.mix.size();

    textFont(createFont("Arial", 16));

    windowName = "Rectangular Window";
}

void draw()
{
    background(0);
    stroke(255);

    for(int i = 0; i < song.fft_l.specSize(); i++)
    {
        // draw the line for frequency band i, scaling it up a bit so we can see it
        rect(i*BAR_WIDTH, height/2, BAR_WIDTH, 0 - song.fft_l.getBand(i)*8);
        rect(i*BAR_WIDTH, height/2, BAR_WIDTH, 0 + song.fft_r.getBand(i)*8);
    }

    for(int i = 0; i < song.song.left.size() - 1; i++)
    {
        line(i, height/4 + song.song.left.get(i)*50,
             i+1, height/4 + song.song.left.get(i+1)*50);
        line(i, 3*height/4 + song.song.right.get(i)*50,
             i+1, 3*height/4 + song.song.right.get(i+1)*50);
    }

    text("The window being used is: " + windowName, 5, 20);
}

void keyReleased() {
    WindowFunction newWindow = FFT.NONE;
    
    if ( key == '1' ) 
    {
        newWindow = FFT.BARTLETT;
    }
    else if ( key == '2' )
    {
        newWindow = FFT.BARTLETTHANN;
    }
    else if ( key == '3' )
    {
        newWindow = FFT.BLACKMAN;
    }
    else if ( key == '4' )
    {
        newWindow = FFT.COSINE;
    }
    else if ( key == '5' )
    {
        newWindow = FFT.GAUSS;
    }
    else if ( key == '6' )
    {
        newWindow = FFT.HAMMING;
    }
    else if ( key == '7' )
    {
        newWindow = FFT.HANN;
    }
    else if ( key == '8' )
    {
        newWindow = FFT.LANCZOS;
    }
    else if ( key == '9' )
    {
        newWindow = FFT.TRIANGULAR;
    }
  
    song.fft_l.window(newWindow);
    song.fft_r.window(newWindow);
    windowName = newWindow.toString();
}

/*
 * Wrapper for a minim AudioPlayer with async two-channel FFT
 */
class Song implements Runnable {
    public AudioPlayer song;
    public int num_samples;
    public FFT fft_l;
    public FFT fft_r;

    public Song(Minim minim, String filename, int num_samples) {
        this.num_samples = num_samples;
        this.song = minim.loadFile(filename, num_samples);
        this.fft_l = new FFT(this.song.bufferSize(), this.song.sampleRate());
        this.fft_r = new FFT(this.song.bufferSize(), this.song.sampleRate());
    }

    @Override
    public void run() {
        this.song.play();

        while (true) {
            this.fft_l.forward(this.song.left);
            this.fft_r.forward(this.song.right);
        }
    }
}
